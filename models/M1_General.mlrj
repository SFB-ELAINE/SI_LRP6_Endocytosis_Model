// General model refers to model variant that only considers ligand
// and receptor interaction.

// Author: Fiete Haack
// Contributed: Kai Budde
// Last edited: 2019/11/19


// +++++++++++++++++++++++
// ++++++ Constants ++++++
// +++++++++++++++++++++++

a:  6.022e23;  // Avogadro constant
vm: 1.37e-14;  // Volume of membrane in (dm)^3 (=1L)

// ++++++++++++++++++++++++++++++++++++
// ++++++ initial species counts ++++++
// ++++++++++++++++++++++++++++++++++++

nR: 4000; // Number of initial cell surface receptors
nL: 2000; // Number of initial extracellular ligands

// ++++++++++++++++++++++++++++++++++++++
// +++++ reaction rate coefficients +++++
// ++++++++++++++++++++++++++++++++++++++

ka: 2.16e6; // L/mol/min // Association rate
kd: 0.02;   // 1/min     // Dissociation rate
ke: 0.05;   // 1/min     // Internalization rate


// ++++++++++++++++++++++++++++++++++++++++++++++++++++++
// +++++ species definitions (number of attributes) +++++
// ++++++++++++++++++++++++++++++++++++++++++++++++++++++

M()[];  // Membrane
R();    // Receptor
L();    // Ligand
LR();   // Ligand-Receptor-Complex
E()[];  // Endosome

// ++++++++++++++++++++++++++++
// +++++ initial solution +++++
// ++++++++++++++++++++++++++++

>>INIT[
 M[
   nL L + 
   nR R + 
   0 LR 
  ] +
 1 E[0 LR]
];

// +++++++++++++++++++++++++++++++
// +++++ reaction rules ++++++++++
// +++++++++++++++++++++++++++++++

// R1/R2: (Un)Binding of ligand to receptor in membrane (ligand concentration is constant)
M[R:r + L:l + sm?] -> M[LR + L + sm?] @ #r * #l * ka / (a*vm);
M[LR:l + sm?]      -> M[R + sm?]      @      #l * kd;

// R3: Internalization of ligand+receptor outside of rafts
M[LR:lr + s?] + E[se?]      -> M[s?] + E[LR+ se?]  @ #lr * ke;







